/*
 * MIT License
 *
 * Copyright (c) 2020 Pinaki Acharya
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 *
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "dev.pinaki.receepee"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    Properties buildProperties = new Properties()
    project.rootProject.file("build.properties").withInputStream { buildProperties.load(it) }

    buildTypes {
        debug {
            buildConfigField "String", 'SERVER_URL', "\"${buildProperties.getProperty("serverUrl")}\""

            buildConfigField "String", 'DATABASE_NAME', "\"${buildProperties.getProperty("databaseName")}\""
            buildConfigField "int", 'DATABASE_VERSION', "${buildProperties.getProperty("databaseVersion")}"

            buildConfigField "String", 'SHARED_PREFS_NAME', "\"${buildProperties.getProperty("sharedPrefsName")}\""
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", 'SERVER_URL', "\"${buildProperties.getProperty("serverUrl")}\""

            buildConfigField "String", 'DATABASE_NAME', "\"${buildProperties.getProperty("databaseName")}\""
            buildConfigField "int", 'DATABASE_VERSION', "${buildProperties.getProperty("databaseVersion")}"

            buildConfigField "String", 'SHARED_PREFS_NAME', "\"${buildProperties.getProperty("sharedPrefsName")}\""
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    // an issue is occurring while using kotlin-coroutines-core
    // and kotlin-coroutines-test dependencies
    // this seems to be fixing the issue
    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/java"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    def coroutine_version = "1.3.7"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutine_version}"

    def nav_version = "2.3.0"
    def room_version = "2.2.5"
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'com.google.android.material:material:1.1.0'

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.recyclerview:recyclerview:1.2.0-alpha04"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.airbnb.android:lottie:3.4.1"
    implementation "io.coil-kt:coil:0.11.0"

    def moshi_version = "1.9.3"
    kapt("com.squareup.moshi:moshi-kotlin-codegen:$moshi_version")
    implementation("com.squareup.moshi:moshi:${moshi_version}")

    def retrofit_version = "2.8.1"
    def okhttp_version = "4.7.2"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:${okhttp_version}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttp_version}"

    def hilt_version = '2.31.2-alpha'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    testImplementation 'junit:junit:4.13'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutine_version}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${coroutine_version}"
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutine_version}"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${coroutine_version}"
}